#!/usr/bin/env fish

set dirs ~/utils ~/apps

if test -f $HOME/.tmux-sessionizer-dirs
  set dirs (cat $HOME/.tmux-sessionizer-dirs)
end

set selected (find \
  $dirs \
  -mindepth 1 \
  -maxdepth 1 \
  -type d \
  -not -name ".*" | fzf)


if test -z $selected
  exit 0
end

set selected_name (basename "$selected" | tr ":., " "____")
set hash (echo $selected | sha256sum | string sub --length 6)
set selected_id (string join "__" $selected_name  $hash)
set tmux_running (pgrep tmux)


function switch_to -a target
  if test -z $TMUX
    tmux attach-session -t $target
  else
    tmux switch-client -t $target
  end
end

function has_session -a name
  return (tmux list-sessions | grep -q "^$name:")
end

function hydrate -a target dir
  if test -f $dir/.tmux-sessionizer
    tmux send-keys -t  $target "source $dir/.tmux-sessionizer" c-M
  else if test -f $HOME/.tmux-sessionizer
    tmux send-keys -t  $target "source $HOME/.tmux-sessionizer" c-M
  end
end

if test -z $TMUX; and test -z $tmux_running
  tmux new-session -s $selected_id -c $selected
  hydrate $selected_id $selected
  exit 0
end

if not has_session $selected_id
  tmux new-session -ds $selected_id -c $selected
  hydrate $selected_id $selected
end

switch_to $selected_id
