#!/usr/bin/env fish

set dirs ~/utils ~/apps

function select_session_directory
  set config_name ".tmux-sessionizer-dirs"

  if test -f $HOME/$config_name
    set dirs (cat $HOME/$config_name)
  end

  set dirs_files (find $dirs \
    -mindepth 2 \
    -maxdepth 2 \
    -type f \
    -name "$config_name")

  for file in $dirs_files
    set base (dirname $file)
    for entry in (cat $file)
      set -a dirs $base/$entry
    end
  end

  echo (find \
    $dirs \
    -mindepth 1 \
    -maxdepth 1 \
    -type d \
    -not -name ".*" | fzf)
end

set selected (select_session_directory)

if test -z $selected
  exit 0
end

set selected_name (basename "$selected" | tr ":., " "____")
set hash (echo $selected | sha256sum | string sub --length 6)
set selected_id (string join "__" $selected_name  $hash)
set tmux_running (pgrep tmux)


function switch_to -a target
  if test -z $TMUX
    tmux attach-session -t $target
  else
    tmux switch-client -t $target
  end
end

function has_session -a name
  return (tmux list-sessions | grep -q "$name:")
end

function hydrate -a target dir
  if test -f $dir/.tmux-sessionizer
    tmux send-keys -t  $target "source $dir/.tmux-sessionizer" c-M
  else if test -f $HOME/.tmux-sessionizer
    tmux send-keys -t  $target "source $HOME/.tmux-sessionizer" c-M
  end
end

if test -z $TMUX; and test -z $tmux_running
  tmux new-session -s $selected_id -c $selected
  hydrate $selected_id $selected
  exit 0
end

if not has_session $selected_id
  tmux new-session -ds $selected_id -c $selected
  hydrate $selected_id $selected
end

switch_to $selected_id
